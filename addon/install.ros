#!/bin/sh
#|-*- mode:lisp -*-|#
#|
exec ros -Q -- $0 "$@"
|#

(ql:quickload '(:quri :ufo :cl-gists) :silent t)

(defun url-body (url)
  (cond ((and (quri:uri-path url) (quri:uri-host url))
	 (format nil "~a~a"
		 (quri:uri-host url)
		 (quri:uri-path url)))
	((quri:uri-host url) (quri:uri-host url))
	((quri:uri-path url) (quri:uri-path url))))

(defun build (ros out)
  (ros:roswell `("build" ,ros "-o" ,out)
	       :interactive nil))

(defun download (url dist)
  (ros:roswell `("roswell-internal-use" "download" ,url ,dist)
	       :interactive nil))

(defun install-http (url)
  (unless (quri:uri-path url) (error 'uiop:subprocess-error))
  (download (quri:render-uri url)
	    (merge-pathnames
	     (file-namestring (quri:uri-path url))
	     (ufo.env:dot-ufo #p"tmp/")))
  (build (merge-pathnames
	  (file-namestring (quri:uri-path url))
	  (ufo.env:dot-ufo #p"tmp/"))
	 (merge-pathnames
	  (pathname-name (quri:uri-path url))
	  (ufo.env:dot-roswell #p"bin/"))))

(defun install-https (url)
  (install-http url))

(defun install-file (url)
  (unless (probe-file (url-body url))
    (error 'uiop:subprocess-error))
  (build  (url-body url)
	  (merge-pathnames
	   (pathname-name (url-body url))
	   (ufo.env:dot-roswell #p"bin/"))))

(defun install-gist (url)
  (unless (quri:uri-path url)
    (error 'uiop:subprocess-error))
  (let ((username (quri:uri-host url))
	(filename (file-namestring (quri:uri-path url))))
    (dolist (gist (cl-gists:list-gists :username username))
      (dolist (file (cl-gists:gist-files gist))
	(when (string= (cl-gists:file-name file) filename)
	  (install-https (quri:uri (cl-gists:file-raw-url file)))
	  (return-from install-gist nil))))
    (error 'uiop:subprocess-error)))

(defun install-ql (url)
  (ros:roswell `("install" ,(quri:uri-host url))
	       :interactive nil))

(defun main (&rest argv)
  (declare (ignorable argv))
  (handler-case
      (dolist (url (mapcar #'quri:uri argv))
	(cond
	  ((string= (quri:uri-scheme url) "gist")
	   (install-gist url))
	  ((string= (quri:uri-scheme url) "file")
	   (install-https url))
	  ((string= (quri:uri-scheme url) "ql")
	   (install-ql url))
	  ((string= (quri:uri-scheme url) "http")
	   (install-http url))
	  ((string= (quri:uri-scheme url) "https")
	   (install-https url))
	  (t (error 'uiop:subprocess-error))))
    (uiop:subprocess-error ()
      (format *error-output* "Install process failed!~%")
      (uiop:quit -1))))
