#!/bin/sh
#|-*- mode:lisp -*-|#
#|
exec ros -Q -- $0 "$@"
|#

(ql:quickload
 '(:quri
   :ufo
   :cl-gists
   :cl-fad
   :trivial-download
   :ZIP) :silent t)

(defun url-body (url)
  (cond ((and (quri:uri-path url) (quri:uri-host url))
	 (format nil "~a~a"
		 (quri:uri-host url)
		 (quri:uri-path url)))
	((quri:uri-host url) (quri:uri-host url))
	((quri:uri-path url) (quri:uri-path url))))

(defun url-file (url)
  (file-namestring (quri:uri-path url)))

(defun build (ros out)
  (ros:roswell `("build" ,ros "-o" ,out)
	       :interactive nil))

(defun install-http (url)
  (unless (and (quri:uri-path url) (quri:uri-host url))
    (error "Invalid URL. "))
  (trivial-download:download
   (quri:render-uri url)
   (merge-pathnames
    (url-file url)
    (ufo:dot-ufo #p"tmp/")))
  (build (merge-pathnames
	  (file-namestring (quri:uri-path url))
	  (ufo:dot-ufo #p"tmp/"))
	 (merge-pathnames
	  (pathname-name (quri:uri-path url))
	  (ufo:dot-ufo #p"addon/"))))

(defun install-https (url)
  (install-http url))

(defun install-file (url)
  (unless (probe-file (url-body url))
    (error "Invalid URL. "))
  (build  (url-body url)
	  (merge-pathnames
	   (pathname-name (url-body url))
	   (ufo:dot-ufo #p"addon/"))))

(defun install-gist (url)
  (unless (and (quri:uri-path url) (quri:uri-host url))
    (error "Invalid URL. "))
  (let ((username (quri:uri-host url))
	(filename (url-file url)))
    (dolist (gist (cl-gists:list-gists :username username))
      (dolist (file (cl-gists:gist-files gist))
	(when (string= (cl-gists:file-name file) filename)
	  (install-https (quri:uri (cl-gists:file-raw-url file)))
	  (return-from install-gist nil))))
    (error 'uiop:subprocess-error)))

(defun install-ql (url)
  (unless (and (not (quri:uri-path url)) (quri:uri-host url))
    (error "Invalid URL. "))
  (ros:roswell `("install" ,(quri:uri-host url))
	       :interactive nil))

(defun main (&rest argv)
  (declare (ignorable argv))
  (handler-case
      (dolist (url (mapcar #'quri:uri argv))
	(cond
	  ((string= (quri:uri-scheme url) "gist")(install-gist url))
	  ((string= (quri:uri-scheme url) "file")(install-file url))
	  ((string= (quri:uri-scheme url) "ql")(install-ql url))
	  ((string= (quri:uri-scheme url) "http")(install-http url))
	  ((string= (quri:uri-scheme url) "https")(install-https url))
	  (t (error "Invalid Schemes"))))
    (error (e) (princ e *error-output*)(terpri)(uiop:quit -1))))
